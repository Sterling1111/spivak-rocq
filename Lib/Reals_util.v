From Lib Require Import Imports Notations.

Open Scope R_scope.

Coercion INR : nat >-> R.
Coercion IZR : Z >-> R.
Coercion Q2R : Q >-> R.

Ltac break_INR :=
  repeat match goal with
  | [ |- context[INR (?n + ?m)] ] =>
      rewrite plus_INR
  | [ |- context[INR (?n * ?m)] ] =>
      rewrite mult_INR
  | [ |- context[INR (S ?n)] ] =>
      rewrite S_INR
  | [ |- context[INR (?n - ?m)] ] =>
      rewrite minus_INR
  | [ |- context[INR (?n ^ ?m)] ] =>
      rewrite pow_INR
  end.

Ltac convert_nat_to_INR_in_H :=
  try
  match goal with
  | [ H: (?x > ?y)%nat |- _ ] => apply lt_INR in H; simpl in H
  | [ H: (?x < ?y)%nat |- _ ] => apply lt_INR in H; simpl in H
  | [ H: (?x >= ?y)%nat |- _ ] => apply le_INR in H; simpl in H
  | [ H: (?x <= ?y)%nat |- _ ] => apply le_INR in H; simpl in H
  | [ H: (?x = ?y)%nat |- _ ] => apply INR_eq in H; simpl in H
  end.

Ltac break_INR_simpl :=
  break_INR; simpl; try field_simplify; try lra; try nra; try nia.

Ltac solve_INR :=
  convert_nat_to_INR_in_H; try field_simplify_eq; try break_INR; simpl; try field; try lra; try nra; try lia.

Ltac solve_abs := 
  try intros; 
  try lra; 
  unfold Rabs in *; 
  repeat match goal with
  | [ |- context[if Rcase_abs ?x then _ else _] ] => 
      destruct (Rcase_abs x); try lra
  | [ H: context[if Rcase_abs ?x then _ else _] |- _ ] => 
      destruct (Rcase_abs x); try lra
  | [ Z := context[if Rcase_abs ?x then _ else _] |- _ ] =>
      destruct (Rcase_abs x); try lra
  end;
  try field; try nra; try nia.

Ltac solve_max := 
  try intros; 
  try lra;
  unfold Rmax, Nat.max in *;
  repeat match goal with
  | [ |- context[if Rle_dec ?x ?y then _ else _] ] => 
      destruct (Rle_dec x y); try lra
  | [ H: context[if Rle_dec ?x ?y then _ else _] |- _ ] => 
      destruct (Rle_dec x y); try lra
  | [ |- context[if le_dec ?x ?y then _ else _] ] =>
      destruct (le_dec x y); try lia
  | [ Z := context[if Rle_dec ?x ?y then _ else _] |- _ ] =>
      destruct (Rle_dec x y); try lra
  | [ Z := context[if le_dec ?x ?y then _ else _] |- _ ] =>
      destruct (le_dec x y); try lia
  end;
  try field; try nra; try nia.

Ltac solve_min :=
  try intros; 
  try lra;
  unfold Rmin, Nat.min in *;
  repeat match goal with
  | [ |- context[if Rle_dec ?x ?y then _ else _] ] => 
      destruct (Rle_dec x y); try lra
  | [ H: context[if Rle_dec ?x ?y then _ else _] |- _ ] => 
      destruct (Rle_dec x y); try lra
  | [ |- context[if le_dec ?x ?y then _ else _] ] =>
      destruct (le_dec x y); try lia
  | [ Z := context[if Rle_dec ?x ?y then _ else _] |- _ ] =>
      destruct (Rle_dec x y); try lra
  | [ Z := context[if le_dec ?x ?y then _ else _] |- _ ] =>
      destruct (le_dec x y); try lia
  end;
  try field; try nra; try nia.

Ltac solve_R :=
  unfold Ensembles.In in *; 
  try solve_INR; 
  try solve_abs; 
  try solve_max; 
  try solve_min; 
  try lra; 
  try tauto; 
  auto.

Lemma pow2_gt_0 : forall r, r <> 0 -> r ^ 2 > 0.
Proof.
  intros r H1. pose proof Rtotal_order r 0 as [H2 | [H2 | H2]]; try nra.
Qed.

Lemma Rmult_le_ge_reg_neg_l :
  forall r r1 r2, r * r1 >= r * r2 -> r < 0 -> r1 <= r2.
Proof. intros; nra. Qed.

Lemma Rmult_ge_le_reg_neg_l :
  forall r r1 r2, r * r1 <= r * r2 -> r < 0 -> r1 >= r2.
Proof. intros; nra. Qed.

Lemma Rabs_triang_3 : forall r1 r2 r3 : R,
  Rabs (r1 + r2 + r3) <= Rabs r1 + Rabs r2 + Rabs r3.
Proof.
  solve_abs.
Qed.

Lemma n_lt_pow2_n : forall n, INR n < 2 ^ n.
Proof.
  induction n as [| k IH].
  - simpl. lra.
  - solve_INR. assert (1 <= 2 ^ k).
    { clear; induction k; simpl; lra. } solve_INR.
Qed.

Lemma Rpow_gt_0 : forall k r,
  r > 0 -> r ^ k > 0.
Proof.
  intros k r H1. induction k as [| k' IH].
  - simpl. lra.
  - simpl. nra.
Qed.

Lemma Rpow_lt_1 : forall r n,
  0 < r < 1 -> (n > 0)%nat -> r ^ n < 1.
Proof.
  intros r n [H1 H2] H3. induction n as [| k IH].
  - lia.
  - simpl. destruct k.
    -- simpl. lra.
    -- assert (r ^ S k < 1) by (apply IH; lia). nra.
Qed.

Lemma Rpow_gt_1 : forall r n,
  r > 1 -> (n > 0)%nat -> r ^ n > 1.
Proof.
  intros r n H1 H2. induction n as [| k IH].
  - lia.
  - simpl. destruct k.
    -- simpl. lra.
    -- assert (r ^ S k > 1) by (apply IH; lia). nra.
Qed.

Lemma Rpow_even_gt_0 : forall r n,
  r <> 0 -> (Nat.Even n) -> r ^ n > 0.
Proof.
  intros r n H1 [k H2].
  rewrite H2. rewrite pow_mult. apply Rpow_gt_0. apply pow2_gt_0. auto.
Qed.

Lemma Rpow_odd_lt_0 : forall r n,
  r < 0 -> (Nat.Odd n) -> r ^ n < 0.
Proof.
  intros r n H1 [k H2].
  rewrite H2. rewrite pow_add, pow_mult. rewrite pow_1. 
  assert (H3 : r^2 > 0). { apply pow2_gt_0; lra. }
  assert (H4 : (r^2)^k > 0). { apply Rpow_gt_0; auto. } nra.
Qed.

Lemma Rdiv_pow_distr : forall r1 r2 n,
  r2 > 0 -> (r1 / r2) ^ n = r1 ^ n / r2 ^ n.
Proof.
  intros r1 r2 n H1. induction n as [| k IH].
  - simpl. lra.
  - simpl. rewrite IH. field. pose proof Rpow_gt_0 k r2 as H2; nra.
Qed.

Lemma Rdiv_lt_1: forall a b : R, a < b -> b > 0 -> a / b < 1.
Proof.
  intros a b H1 H2.
  apply Rmult_lt_compat_r with (r := 1/b) in H1.
  - replace (a * (1 / b)) with (a / b) in H1 by lra.
    replace (b * (1 / b)) with 1 in H1 by (field; lra).
    apply H1.
  - apply Rdiv_pos_pos. lra. apply H2.
Qed.  

Lemma pow_incrst_1 : forall r1 r2 n,
  (n > 0)%nat -> r1 >= 0 -> r2 > 0 -> r1 < r2 -> r1^n < r2^n.
Proof.
  intros r1 r2 n H1 H2 H3 H4. generalize dependent r1. generalize dependent r2. induction n as [| k IH].
  - lia.
  - intros r2 H2 r1 H3 H4. simpl. destruct k; try nra. assert (H6 : r1^(S k) < r2 ^(S k)). { apply IH; try lia; try nra. }
    apply Rmult_ge_0_gt_0_lt_compat; try nra. simpl. assert (0 <= r1 ^ k). { apply pow_le; nra. } nra. 
Qed.

Lemma Rlt_pow_base : forall a b n,
  0 <= a -> 0 < b -> (n > 0)%nat -> a^n < b^n -> a < b.
Proof.
  intros a b n H1 H2 H3 H4. induction n as [| k IH].
  - lia.
  - simpl in H4. destruct k.
    -- simpl in H4. lra.
    -- apply IH. lia. simpl. simpl in H4. pose proof Rtotal_order a b as [H5 | [H6 | H7]].
      --- assert (k = 0 \/ k > 0)%nat as [H8 | H8] by lia; subst; try lra.
          assert (H6 : a^k < b^k). { apply pow_incrst_1; try lia; try lra. } assert (H7 : 0 <= a^k). { apply pow_le. lra. } nra.
      --- subst; lra.
      --- assert (k = 0 \/ k > 0)%nat as [H8 | H8] by lia. { rewrite H8 in H4. nra. }
          assert (H9 : b^k < a^k). { apply pow_incrst_1; try lia; try lra. } assert (H10 : b^k > 0). { apply pow_lt. lra. }
          assert (H11 : a * a^k > b * b^k). { nra. } assert (H12 : a * (a * a^k) > b * (b * b^k)). { apply Rmult_gt_0_lt_compat. nra. nra. nra. nra. } nra.
Qed.

Lemma sqrt_2_gt_1 : (1 < sqrt 2)%R.
Proof.
    apply Rlt_pow_base with (n := 2%nat); try lra; try lia. apply sqrt_lt_R0; lra. rewrite pow2_sqrt; lra.
Qed.

Lemma Rinv_lt_contravar_3 : forall a b c,
  a > 0 -> b > 0 -> c > 0 -> a < b < c -> 1 / c < 1 / b < 1 / a.
Proof.
  intros a b c H1 H2 H3 [H4 H5]. split; repeat rewrite Rdiv_1_l; apply Rinv_lt_contravar; nra.
Qed.

Lemma nat_pos_Rpos_iff : ∀ n : nat,
  n > 0 <-> (n > 0)%nat.
Proof.
  intros n; split; intros H1; solve_R.
  apply INR_lt. solve_R.
Qed.

Lemma nat_gt_Rgt_iff : ∀ n m : nat,
  INR n > INR m <-> (n > INR m).
Proof.
  intros n m. solve_R.
Qed.

Ltac compare_elems e1 e2 := 
  let e1' := eval simpl in e1 in
  let e2' := eval simpl in e2 in 
  field_simplify; try nra; try nia.

(* Compare two lists recursively element by element *)
Ltac compare_lists_step :=
  match goal with
  | [ |- [] = [] ] => reflexivity
  | [ |- (?x :: ?xs) = (?y :: ?ys) ] => 
      first [
        assert (x = y) by compare_elems x y;
        apply f_equal2; [assumption | compare_lists_step]
        |
        fail "Elements" x "and" y "cannot be proven equal"
      ]
  | [ |- ?l1 = ?l2 ] =>
      fail "Lists" l1 "and" l2 "have different lengths or structures"
  end.

Ltac auto_list :=
  intros; compute;
  try solve [reflexivity];
  compare_lists_step.

Definition is_natural (r : R) : Prop :=
    exists n : nat, r = INR n.

Definition is_integer (r : R) : Prop :=
    exists z : Z, r = IZR z.

Lemma not_integer : forall r n,
  IZR n < r < IZR (n + 1) -> ~ is_integer r.
Proof.
  intros r n [H1 H2] [m H3]. rewrite H3 in H1, H2. apply lt_IZR in H1, H2. lia.
Qed.

Lemma is_natural_plus : forall r1 r2 : R,
  is_natural r1 -> is_natural r2 -> is_natural (r1 + r2).
Proof.
  intros r1 r2 H1 H2. destruct H1 as [n1 H1]. destruct H2 as [n2 H2]. exists (n1 + n2)%nat. rewrite H1, H2. rewrite plus_INR. reflexivity.
Qed.

Lemma is_natural_mult : forall r1 r2 : R,
  is_natural r1 -> is_natural r2 -> is_natural (r1 * r2).
Proof.
  intros r1 r2 H1 H2. destruct H1 as [n1 H1]. destruct H2 as [n2 H2]. exists (n1 * n2)%nat. rewrite H1, H2. rewrite mult_INR. reflexivity.
Qed. 

Lemma is_natural_pow : forall r n,
  is_natural r -> is_natural (r ^ n).
Proof.
  intros r n H1. destruct H1 as [k H1]. exists (k ^ n)%nat. rewrite H1. rewrite pow_INR. reflexivity.
Qed.

Lemma is_integer_plus : forall r1 r2 : R,
  is_integer r1 -> is_integer r2 -> is_integer (r1 + r2).
Proof.
  intros r1 r2 H1 H2. destruct H1 as [z1 H1]. destruct H2 as [z2 H2]. exists (z1 + z2)%Z. rewrite H1, H2. rewrite plus_IZR. reflexivity.
Qed.

Lemma is_integer_mult : forall r1 r2 : R,
  is_integer r1 -> is_integer r2 -> is_integer (r1 * r2).
Proof.
  intros r1 r2 H1 H2. destruct H1 as [z1 H1]. destruct H2 as [z2 H2]. exists (z1 * z2)%Z. rewrite H1, H2. rewrite mult_IZR. reflexivity.
Qed.

Lemma is_integer_pow : forall r n,
  is_integer r -> is_integer (r ^ n).
Proof.
  intros r n H1. induction n as [| k IH].
  - simpl. exists 1%Z. reflexivity.
  - simpl. apply is_integer_mult; try apply IH; auto.
Qed.

Lemma pow_div_sub : forall x n1 n2,
  x <> 0 -> (n2 >= n1)%nat -> x ^ (n2 - n1)%nat = x ^ n2 / x ^ n1.
Proof.
  intros x n1 n2 H1 H3. generalize dependent n1. induction n2 as [| k IH].
  - intros n1 H2. replace (n1) with 0%nat by lia. simpl. lra.
  - intros n1 H2. destruct (Nat.eq_dec n1 (S k)).
    + subst. simpl. rewrite Nat.sub_diag. field. split; auto. apply pow_nonzero; auto.
    + assert (H3 : (n1 <= k)%nat) by lia. replace (S k - n1)%nat with (S (k - n1)) by lia.
      simpl. rewrite IH; auto. field; apply pow_nonzero; auto.
Qed.

Lemma nltb_gt : forall a b : nat, (a > b)%nat <-> (a <=? b) = false.
Proof.
  intros a b. split.
  - intros H1. apply leb_correct_conv; lia.
  - intros H1. destruct (Nat.leb_spec a b); try lia. 
Qed.

Lemma nltb_ge : forall a b : nat, (a >= b)%nat <-> (a <? b) = false.
Proof.
  intros a b. split.
  - intros H1. apply leb_correct_conv; lia.
  - intros H1. destruct (Nat.ltb_spec a b); try lia.
Qed.

Lemma Rmult_gt_reg_neg_r : forall r r1 r2,
  r < 0 -> r1 * r > r2 * r -> r1 < r2.
Proof.
  intros. nra.
Qed.

Lemma cross_multiply_lt : forall r1 r2 r3 r4,
  r1 > 0 -> r2 > 0 -> r3 > 0 -> r4 > 0 ->
  r1 * r4 < r2 * r3 -> r1 / r2 < r3 / r4.
Proof.
  intros r1 r2 r3 r4 H1 H2 H3 H4 H5. apply Rmult_lt_reg_r with (r := r2); 
  apply Rmult_lt_reg_r with (r := r4); field_simplify; nra.
Qed.

Lemma gte_1_INR : forall n : nat,
  0 < n -> 1 <= n.
Proof.
  intros n H1. assert ((INR n = 1) \/ (INR n > 1)) as [H2 | H2]; try lra.
  {
    replace 0 with (INR 0) in * by auto. replace 1 with (INR 1) in * by auto. destruct n.
    - apply INR_lt in H1; lia.
    - apply INR_lt in H1. destruct n; [left; reflexivity | right; apply lt_INR; lia ].
  }
Qed.

Lemma Rdiv_neg_neg_eq : forall r1 r2,
  -r1 / -r2 = r1 / r2.
Proof.
  intros r1 r2. destruct (Req_dec_T r1 0) as [H1 | H1]; destruct (Req_dec_T r2 0) as [H2 | H2]; try nra.
  - rewrite H2, Ropp_0, Rdiv_0_r, Rdiv_0_r. reflexivity.
  - field; auto.
Qed.

Lemma Rdiv_neq_0 : forall r1 r2,
  r2 <> 0 -> r1 / r2 <> 0 <-> r1 <> 0.
Proof.
  intros r1 r2 H1. split; intros H2.
  - intros H3. rewrite H3 in H2. lra.
  - intros H3. pose proof Rtotal_order r1 0 as [H4 | [H4 | H4]]; pose proof Rtotal_order r2 0 as [H5 | [H5 | H5]]; try nra.
    + pose proof Rdiv_neg_neg r1 r2 ltac:(lra) ltac:(lra). nra.
    + pose proof Rdiv_neg_pos r1 r2 ltac:(lra) ltac:(lra). nra.
    + pose proof Rdiv_pos_neg r1 r2 ltac:(lra) ltac:(lra). nra.
    + pose proof Rdiv_pos_pos r1 r2 ltac:(lra) ltac:(lra). nra.
Qed.

Lemma Rdiv_pos_pos_rev : forall r1 r2,
  r1 / r2 > 0 -> r2 > 0 -> r1 > 0.
Proof.
  intros r1 r2 H1 H2. pose proof Rtotal_order r1 0 as [H3 | [H3 | H3]]; try nra.
  pose proof Rdiv_neg_pos r1 r2 ltac:(lra) ltac:(lra). nra.
Qed.

Lemma Rdiv_neg_pos_rev : forall r1 r2,
  r1 / r2 < 0 -> r2 > 0 -> r1 < 0.
Proof.
  intros r1 r2 H1 H2. pose proof Rtotal_order r1 0 as [H3 | [H3 | H3]]; try nra.
  pose proof Rdiv_pos_pos r1 r2 ltac:(lra) ltac:(lra). nra.
Qed.

Lemma Rdiv_pos_neg_rev : forall r1 r2,
  r1 / r2 < 0 -> r2 < 0 -> r1 > 0.
Proof.
  intros r1 r2 H1 H2. pose proof Rtotal_order r1 0 as [H3 | [H3 | H3]]; try nra.
  pose proof Rdiv_neg_neg r1 r2 ltac:(lra) ltac:(lra). nra.
Qed.

Lemma Rdiv_pos_neg_rev' : forall r1 r2,
  r1 / r2 > 0 -> r2 < 0 -> r1 < 0.
Proof.
  intros r1 r2 H1 H2. pose proof Rtotal_order r1 0 as [H3 | [H3 | H3]]; try nra.
  pose proof Rdiv_pos_neg r1 r2 ltac:(lra) ltac:(lra). nra.
Qed.

Lemma Rdiv_nonneg_nonneg : forall r1 r2,
  r1 >= 0 -> r2 > 0 -> r1 / r2 >= 0.
Proof.
  intros r1 r2 H1 H2.
  pose proof Rtotal_order r1 0 as [H3 | [H3 | H3]]; try nra.
  pose proof Rdiv_pos_pos r1 r2 ltac:(lra) ltac:(lra). nra.
Qed.  

Lemma floor_spec : ∀ x : R,
  x >= 0 ->
  ⌊ x ⌋ <= x < ⌊ x ⌋ + 1.
Proof.
  intros x H1.
  unfold Nfloor.
  rewrite INR_IZR_INZ.
  generalize (base_Int_part x). intros [H2 H3].
  rewrite Z2Nat.id; [split; lra | ].
  assert (H4 : IZR (Int_part x) > -1) by lra.
  apply lt_IZR in H4. lia.
Qed.

Lemma ceil_spec : forall x : R,
  x > 0 ->
  ⌈ x ⌉ - 1 <= x < ⌈ x ⌉.
Proof.
  intros x H1.
  unfold Nceil.
  rewrite INR_IZR_INZ.
  generalize (archimed x); intros [H2 H3].
  rewrite Z2Nat.id; [ split; lra |].
  apply le_IZR; lra.
Qed.

Lemma floor_unique : forall (x : R) (n : nat),
  x >= 0 ->
  n <= x < n + 1 ->
  ⌊ x ⌋ = n.
Proof.
  intros x n H1 [H2 H3].
  unfold Nfloor.
  apply Nat2Z.inj.
  rewrite Z2Nat.id.
  - symmetry.
    apply Int_part_spec.
    rewrite <- INR_IZR_INZ.
    split; lra.
  - generalize (base_Int_part x); intros [H4 H5].
    assert (H6 : (-1 < Int_part x)%Z).
    { apply lt_IZR. lra. }
    lia.
Qed.

Lemma ceil_unique : forall (x : R) (n : nat),
  x > 0 ->
  n - 1 <= x < n ->
  ⌈ x ⌉ = n.
Proof.
  intros x n H1 [H2 H3].
  unfold Nceil.
  apply Nat2Z.inj.
  rewrite Z2Nat.id.
  - symmetry. apply tech_up; rewrite <- INR_IZR_INZ; lra.
  - apply le_IZR.
    transitivity x; [lra |].
    generalize (archimed x); intros [H4 H5]. lra.
Qed.

Lemma floor_INR : forall r, is_natural r -> INR ⌊r⌋ = r.
Proof.
  intros r [k H1].
  rewrite H1. unfold Nfloor. rewrite Int_part_INR.
  rewrite Nat2Z.id; auto.
Qed.

Lemma floor_INR_id : forall n : nat, ⌊INR n⌋ = n.
Proof.
  intros n. unfold Nfloor. rewrite Int_part_INR.
  rewrite Nat2Z.id; auto.
Qed.

Lemma floor_power_succ_div : forall n b, b > 1 -> is_natural b -> ⌊b ^ (S n) / b⌋ = ⌊b ^ n⌋.
Proof.
  intros n b H1 [k H2]. assert ((k = 0)%nat \/ (k > 0)%nat) as [H3 | H3] by lia.
  - subst. replace 1 with (INR 1) in H1 by auto. apply INR_lt in H1. lia.
  - rewrite H2, <- tech_pow_Rmult. 
    replace (INR k * INR k ^ n / INR k) with (INR k ^ n) by (field; lra). auto.
Qed.

Lemma floor_power_succ_ge_base : forall b k, 
  b > 1 -> is_natural b -> ⌊b ^ (S k)⌋ >= b.
Proof.
  intros b k H1 H2.
  induction k as [| k IH].
  - simpl. rewrite floor_INR. 2: { rewrite Rmult_1_r; auto. } lra.
  - rewrite floor_INR in *; try apply is_natural_pow; auto. simpl in *; nra.
Qed.

Lemma floor_floor : forall r, r >= 0 -> ⌊⌊r⌋⌋ = ⌊r⌋.
Proof.
  intros r H1.
  apply floor_unique; try lra.
  pose proof floor_spec r H1 as [H2 H3].
  pose proof pos_INR (⌊r⌋) as H4.
  lra.
Qed.

Lemma pow_unbounded : forall b M : R, b > 1 -> exists n : nat, b ^ n >= M.
Proof.
  intros b M H1.
  destruct (Pow_x_infinity b ltac:(solve_R) M) as [N H2].
  exists N.
  specialize (H2 N ltac:(solve_R)).
  rewrite Rabs_right in H2; solve_R.
  apply Rle_ge. apply pow_le. lra.
Qed.

Lemma floor_gt_0 : ∀ x : R, x ≥ 1 → ⌊x⌋ > 0.
Proof.
  intros x H1. replace 0 with (INR 0) by auto.
  destruct (floor_spec x ltac:(lra)) as [H2 H3].
  solve_R.
Qed.

Lemma Rdiv_ge_0 : forall a b,
  a >= 0 -> b > 0 -> a / b >= 0.
Proof.
  intros a b H1 H2.
  pose proof Rtotal_order a 0 as [H3 | [H3 | H3]]; try nra.
  pose proof Rdiv_pos_pos a b ltac:(lra) ltac:(lra). nra.
Qed.

Lemma Rle_div_l : ∀ a b c, c > 0 → a / c ≤ b ↔ a ≤ b * c.
Proof.
  intros a b c H.
  split; intros H1.
  - apply Rmult_le_reg_r with (/ c); [apply Rinv_0_lt_compat; lra |].
    rewrite Rmult_assoc, Rinv_r, Rmult_1_r; nra.
  - apply Rmult_le_reg_r with c; [lra |].
    rewrite Rdiv_def, Rmult_assoc, Rinv_l, Rmult_1_r; nra.
Qed.

Lemma Rle_div_r : ∀ a b c, c > 0 → a ≤ b / c ↔ a * c ≤ b.
Proof.
  intros a b c H.
  split; intros H1.
  - apply Rmult_le_reg_r with (/ c); [apply Rinv_0_lt_compat; lra |].
    rewrite Rmult_assoc, Rinv_r, Rmult_1_r; nra.
  - apply Rmult_le_reg_r with c; [lra |].
    rewrite Rdiv_def, Rmult_assoc, Rinv_l, Rmult_1_r; nra.
Qed.

Lemma Rlt_div_l : ∀ a b c, c > 0 → a / c < b ↔ a < b * c.
Proof.
  intros a b c H.
  split; intros H1.
  - apply Rmult_lt_reg_r with (/ c); [apply Rinv_0_lt_compat; lra |].
    rewrite Rmult_assoc, Rinv_r, Rmult_1_r; nra.
  - apply Rmult_lt_reg_r with c; [lra |].
    rewrite Rdiv_def, Rmult_assoc, Rinv_l, Rmult_1_r; nra.
Qed.

Lemma floor_div_general : forall n d : nat, 
  (d > 0)%nat -> 
  (n / d)%nat = ⌊INR n / INR d⌋.
Proof.
  intros n d H1.
  symmetry.
  apply floor_unique.
  - apply Rdiv_ge_0; solve_R. apply Rle_ge, pos_INR.
  - split.
    + apply Rle_div_r; solve_R.
      rewrite <- mult_INR. apply le_INR.
      rewrite Nat.mul_comm.
      apply Nat.Div0.mul_div_le; auto.
    + apply Rlt_div_l; solve_R.
      rewrite Rmult_plus_distr_r, Rmult_1_l, <- mult_INR, <- plus_INR.
      Set Printing Coercions.
      apply lt_INR.
      rewrite Nat.mul_comm.
      assert (H2 : (d > 0)%nat).
      { 
        replace 0 with (INR 0) in H1 by reflexivity.
        apply INR_lt in H1. 
        exact H1. 
      }
      pose proof Nat.div_mod n d ltac:(lia) as H3.
      pose proof Nat.mod_upper_bound n d ltac:(lia) as H4.
      lia.
Qed.

Lemma Rle_not_gt : forall a b,
  ~(a > b) <-> a <= b.
Proof.
  intros a b. nra.
Qed.

Lemma INR_ge : forall n m : nat,
  INR n >= INR m <-> (n >= m)%nat.
Proof.
  intros n m. split.
  - intros H1. apply Rge_le, INR_le in H1. lia.
  - intros H1. solve_R.
Qed.

Lemma iter_ineq_on_powers
  (a b c1 c2 : R) (f : nat -> R)
  (M n j : nat) :
  a ≥ 1 ->
  b > 1 ->
  c1 > 0 ->
  is_natural b ->
  (forall m : nat, m ≥ c2 -> a * f ⌊m / b⌋ ≤ c1 * f m) ->
  b ^ M ≥ Rmax c2 b ->
  (M > 0)%nat ->
  (0 <= j <= n - M - 1)%nat ->
  a ^ j * f ⌊b ^ (n - j)⌋ ≤ c1 ^ j * f ⌊b ^ n⌋.
Proof.
  intros H1 H2 H0 H3 H4 H5 H6 H7.
  induction j as [| k IH].
  - rewrite Nat.sub_0_r. lra.
  - assert (H8 : (0 <= k <= n - M - 1)%nat) by lia.
    specialize (IH H8). pose proof H3 as H3'.
    destruct H3 as [nb H3].
  assert (H9 : (n - k = S (n - S k))%nat) by lia.
  assert (H10 : is_natural (b ^ (n - k))).
  { exists (nb ^ (n - k))%nat. rewrite H3, pow_INR. reflexivity. }
  assert (H11 : INR ⌊b ^ (n - k)⌋ >= c2).
  { rewrite floor_INR; try exact H10.
    apply Rle_ge, Rle_trans with (b ^ M); [ solve_R | ]. 
    apply Rle_pow; try lra. lia. }
  specialize (H4 _ H11).
  rewrite floor_INR in H4; try exact H10.
  rewrite H9 in H4.
  rewrite floor_power_succ_div in H4; auto.
  apply Rle_trans with (a ^ k * (c1 * f ⌊b ^ (n - k)⌋)).
  + replace (a ^ S k * f ⌊b ^ (n - S k)⌋) with (a ^ k * (a * f ⌊b ^ (n - S k)⌋)) by (simpl; lra).
    apply Rmult_le_compat_l. apply pow_le. lra.
    rewrite H9. auto.
  + replace (a ^ k * (c1 * f ⌊b ^ (n - k)⌋)) with (c1 * (a ^ k * f ⌊b ^ (n - k)⌋)) by lra.
    replace (c1 ^ S k * f ⌊b ^ n⌋) with (c1 * (c1 ^ k * f ⌊b ^ n⌋)) by (simpl; lra).
    apply Rmult_le_compat_l; lra.
Qed.

Lemma no_natural_between : forall (n : ℕ) r,
  n < r < (S n) -> ~ is_natural r.
Proof.
  intros n r [H1 H2] [k H3]. rewrite H3 in *. apply INR_lt in H1. apply INR_lt in H2. lia.
Qed.

Lemma no_integer_between : forall (n : ℤ) r,
  n < r < n + 1 -> ~ is_integer r.
Proof.
  intros n r [H1 H2] [k H3]. rewrite H3 in *. apply lt_IZR in H1. rewrite <- plus_IZR in H2. apply lt_IZR in H2. lia.
Qed.

Lemma Rabs_div : forall r1 r2,
  |r1 / r2| = |r1| / |r2|.
Proof.
  intros r1 r2.
  destruct (Rtotal_order r1 r2) as [H1 | [H1 | H1]];
  destruct (Rtotal_order r2 0) as [H2 | [H2 | H2]]; 
  destruct (Rtotal_order r1 0) as [H3 | [H3 | H3]];
  subst; try solve [solve_R].
  - solve_R. pose proof Rdiv_neg_neg r1 r2 ltac:(lra) ltac:(lra). nra.
  - repeat rewrite Rabs_R0, Rdiv_0_r; reflexivity.
  - pose proof Rdiv_neg_pos r1 r2 ltac:(lra) ltac:(lra). solve_R.
  - pose proof Rdiv_pos_pos r1 r2 ltac:(lra) ltac:(lra). solve_R.
  - rewrite Rdiv_diag; solve_R.
  - rewrite Rabs_R0, Rdiv_0_r. solve_R.
  - rewrite Rdiv_diag; solve_R.
  - pose proof Rdiv_neg_neg r1 r2 ltac:(lra) ltac:(lra). solve_R.
  - pose proof Rdiv_pos_neg r1 r2 ltac:(lra) ltac:(lra). solve_R.
  - repeat rewrite Rabs_R0, Rdiv_0_r; reflexivity.
  - pose proof Rdiv_pos_pos r1 r2 ltac:(lra) ltac:(lra). solve_R.
Qed.

Lemma pow_over_factorial_tends_to_0 : ∀ x ε,
  x > 0 -> ε > 0 -> ∃ n, x^n / n! < ε.
Proof.
  intros x ε H1 H2.
  set (n0 := (⌊ 2 * x ⌋ + 1)%nat).
  assert (H3 : INR n0 >= 2 * x).
  {
    unfold n0. rewrite plus_INR. simpl.
    pose proof floor_spec (2 * x) ltac:(lra) as [H4 H5].
    lra.
  }
  set (C := x ^ n0 / INR (fact n0)).
  assert (H4 : exists k : nat, 2 ^ k > C / ε).
  {
    destruct (pow_unbounded 2 (C / ε + 1) ltac:(lra)) as [k H4].
    exists k. lra.
  }
  destruct H4 as [k H4].
  exists (n0 + k)%nat.
  
  assert (H5 : x^(n0 + k) / INR (fact (n0 + k)) <= (1/2)^k * C).
  {
    unfold C.
    clear H4 H2.
    induction k as [| k H6].
    - replace (n0 + 0)%nat with n0 by lia.
      simpl. lra.
    - replace (n0 + S k)%nat with (S (n0 + k)) by lia.
      simpl (fact (S (n0 + k))).
      pose proof (INR_fact_lt_0 (n0 + k)) as H7.
      assert (H8 : 0 < INR (S (n0 + k))).
      { rewrite S_INR; pose proof pos_INR (n0 + k) as H8; lra. }
      assert (H9 : x / INR (S (n0 + k)) <= 1 / 2).
      {
        apply Rmult_le_reg_r with (r := INR (S (n0 + k)) * 2); [apply Rmult_lt_0_compat; lra |].
        replace (x / INR (S (n0 + k)) * (INR (S (n0 + k)) * 2)) with (2 * x) by (field; lra).
        replace (1 / 2 * (INR (S (n0 + k)) * 2)) with (INR (S (n0 + k))) by (field; lra).
        rewrite S_INR, plus_INR.
        pose proof pos_INR k as H10.
        lra.
      }
      replace (INR ((n0 + k)! + (n0 + k) * (n0 + k)!)) with (INR (S (n0 + k)) * INR ((n0 + k)!)) by solve_R.
      replace (x ^ S (n0 + k) / (INR (S (n0 + k)) * INR ((n0 + k)!))) with ((x / INR (S (n0 + k))) * (x ^ (n0 + k) / INR ((n0 + k)!))).
      2 : { solve_R. split. apply INR_fact_neq_0. pose proof pos_INR n0. pose proof pos_INR k. lra. }
      replace ((1 / 2) ^ S k * (x ^ n0 / INR (n0!))) with ((1 / 2) * ((1 / 2) ^ k * (x ^ n0 / INR (n0!)))).
      2 : { solve_R. apply INR_fact_neq_0. }
      apply Rle_trans with ((1 / 2) * (x ^ (n0 + k) / INR ((n0 + k)!))); try lra.
      apply Rmult_le_compat_r; auto. apply Rlt_le, Rdiv_pos_pos; auto. apply Rpow_gt_0; lra.
  }
  
  apply Rle_lt_trans with ((1 / 2) ^ k * C); auto.
  assert (H6 : 2 ^ k > 0). { apply Rpow_gt_0; lra. }
  assert (H7 : (1 / 2) ^ k = 1 / 2 ^ k). { rewrite Rdiv_pow_distr, pow1; lra. }
  rewrite H7.
  apply Rmult_lt_reg_r with (r := 2 ^ k / ε). apply Rdiv_pos_pos; auto.
  field_simplify; try lra.
Qed.

Lemma Rpower_gt_0 : forall r n,
  r > 0 -> Rpower r n > 0.
Proof.
  intros r n H1. unfold Rpower. destruct (Rle_dec 0 r).
  - apply exp_pos.
  - apply exp_pos.
Qed.

Lemma Rpow_neq_0 : forall r n,
  r <> 0 -> r ^ n <> 0.
Proof.
  intros r n H1. induction n as [| n' IH].
  - simpl. nra.
  - simpl. nra.
Qed.

Lemma Rpow_div_l : forall r1 r2 k,
  r2 <> 0 -> r2 <> 0 -> (r1 / r2) ^ k = r1 ^ k / r2 ^ k.
Proof.
  intros r1 r2 k H1 H2. induction k as [| k' IH].
  - simpl. field.
  - simpl. rewrite IH. field. split. 2 : { nra. }
    apply Rpow_neq_0. auto.
Qed.

Lemma Rpow_1_k : forall k,
  1 ^ k = 1.
Proof.
  intros k. induction k as [| k' IH].
  - simpl. reflexivity.
  - simpl. rewrite IH. lra.
Qed.

Lemma pow_neg1_n_plus_1 : forall (n : nat),
  (-1) ^ (n + 1) = (-1) ^ n * (-1) ^ 1.
Proof.
  intros n.
  induction n.
  - simpl. lra.
  - simpl. rewrite IHn. lra.
Qed.

Lemma pow_neg1_n_plus_n : forall (n : nat),
  (-1)^(n+n) = (-1)^n * (-1)^n.
Proof.
  induction n as [| k IH].
  - simpl. lra.
  - replace ((-1)^(S k)) with ((-1)^k * (-1)^1) by ((simpl; lra)).
    replace ((-1)^1) with (-1) by lra.
    replace ((-1) ^ k * -1 * ((-1) ^ k * -1)) with ((-1) ^ k * (-1) ^ k) by lra.
    rewrite <- IH.
    replace ((S k + S k))%nat with (((k + k) + 1) + 1)%nat by lia.
    repeat rewrite pow_neg1_n_plus_1. lra. 
Qed.

Lemma pow_neg1_n : forall (n : nat),
  (-1)^n = 1 \/ (-1)^n = -1.
Proof.
  intros n.
  induction n as [| k IH].
  - simpl. left. lra.
  - destruct IH as [H | H].
    + right. replace (S k) with (k + 1)%nat by lia. rewrite pow_neg1_n_plus_1. rewrite H. lra.
    + left. replace (S k) with (k + 1)%nat by lia. rewrite pow_neg1_n_plus_1. rewrite H. lra.
Qed.

Lemma r_mult_r_is_Rsqr : forall r : R, r * r = Rsqr r.
Proof.
  intros r.
  unfold Rsqr. reflexivity.
Qed.

Lemma pow_neg1_odd : forall k, Nat.Odd k -> (-1) ^ k = -1.
Proof.
  intros k Hodd.
  destruct Hodd as [m Heq]. rewrite Heq.
  rewrite pow_neg1_n_plus_1. simpl.
  replace ((m + (m + 0))%nat) with (m + m)%nat by lia.
  rewrite Rmult_1_r. rewrite pow_neg1_n_plus_n. 
  rewrite r_mult_r_is_Rsqr. assert (H : (0 <= ((-1) ^ m)²)) by apply Rle_0_sqr.
  destruct (pow_neg1_n m) as [Hpos | Hneg].
  - rewrite Hpos. unfold Rsqr. lra.
  - rewrite Hneg. unfold Rsqr. lra.
Qed.

Lemma pow_neg1_even : forall k, Nat.Even k -> (-1) ^ k = 1.
Proof.
  intros k. unfold Nat.Even. intros [m Heq].
  assert (H2: (-1) ^ (2 * m + 1) = -1).
  { apply pow_neg1_odd. exists m. reflexivity. } 
  rewrite Heq. rewrite pow_neg1_n_plus_1 in H2. lra.
Qed.

Lemma mult_IZR_eq_1 : forall z1 z2,
  IZR z1 * IZR z2 = 1 -> ((z1 = 1 /\ z2 = 1) \/ (z1 = -1 /\ z2 = -1))%Z.
Proof.
  intros z1 z2 H1. assert (z1 = 0 \/ z1 = 1 \/ z1 = -1 \/ z1 < -1 \/ z1 > 1)%Z as [H2 | [H2 | [H2 | [H2 | H2]]]] by lia.
  - rewrite H2 in H1. lra.
  - rewrite H2 in H1. rewrite Rmult_1_l in H1. apply eq_IZR in H1. lia.
  - rewrite H2 in H1. simpl in H1. assert (IZR z2 = -1) as H3 by lra. apply eq_IZR in H3. lia.
  - rewrite <- mult_IZR in H1. apply eq_IZR in H1. pose proof Ztrichotomy z2 0 as [H3 | [H3 | H3]]; lia.
  - rewrite <- mult_IZR in H1. apply eq_IZR in H1. pose proof Ztrichotomy z2 0 as [H3 | [H3 | H3]]; lia.
Qed.

Lemma mult_IZR_eq_neg1 : forall z1 z2,
  IZR z1 * IZR z2 = -1 -> ((z1 = 1 /\ z2 = -1) \/ (z1 = -1 /\ z2 = 1))%Z.
Proof.
  intros z1 z2 H1. assert (z1 = 0 \/ z1 = 1 \/ z1 = -1 \/ z1 < -1 \/ z1 > 1)%Z as [H2 | [H2 | [H2 | [H2 | H2]]]] by lia.
  - rewrite H2 in H1. lra.
  - rewrite H2 in H1. rewrite Rmult_1_l in H1. apply eq_IZR in H1. lia.
  - rewrite H2 in H1. simpl in H1. assert (IZR z2 = 1) as H3 by lra. apply eq_IZR in H3. lia.
  - rewrite <- mult_IZR in H1. apply eq_IZR in H1. pose proof Ztrichotomy z2 0 as [H3 | [H3 | H3]]; lia.
  - rewrite <- mult_IZR in H1. apply eq_IZR in H1. pose proof Ztrichotomy z2 0 as [H3 | [H3 | H3]]; lia.
Qed.

Open Scope Z_scope.

Lemma Zmult_eq_1 : forall z1 z2,
  z1 * z2 = 1 -> ((z1 = 1 /\ z2 = 1) \/ (z1 = -1 /\ z2 = -1))%Z.
Proof.
  intros z1 z2 H1. pose proof Ztrichotomy z1 0 as [H2 | [H2 | H2]]; pose proof Ztrichotomy z2 0 as [H3 | [H3 | H3]]; lia.
Qed.

Lemma Zmult_eq_neg1 : forall z1 z2,
  z1 * z2 = -1 -> ((z1 = 1 /\ z2 = -1) \/ (z1 = -1 /\ z2 = 1))%Z.
Proof.
  intros z1 z2 H1. pose proof Ztrichotomy z1 0 as [H2 | [H2 | H2]]; pose proof Ztrichotomy z2 0 as [H3 | [H3 | H3]]; lia.
Qed.

Lemma Even_ZEven : forall n,
  Nat.Even n <-> Z.Even (Z.of_nat n).
Proof.
  intros n; split.
  - intros [m H1]. exists (Z.of_nat m). lia.
  - intros [m H1]. exists (Z.to_nat m). lia.
Qed.

Lemma Odd_ZOdd : forall n,
  Nat.Odd n <-> Z.Odd (Z.of_nat n).
Proof.
  intros n; split.
  - intros [m H1]. exists (Z.of_nat m). lia.
  - intros [m H1]. exists (Z.to_nat m). lia.
Qed.

Lemma Zpow_neg1_nat_even : forall n : nat,
  Nat.Even n -> (-1) ^ (Z.of_nat n) = 1.
Proof.
  intros n H1. apply Even_ZEven in H1. rewrite <- Z.pow_opp_even. simpl. rewrite Z.pow_1_l; lia. auto.
Qed.

Lemma Zpow_neg1_nat_odd : forall n : nat,
  Nat.Odd n -> (-1) ^ (Z.of_nat n) = -1.
Proof.
  intros n H1. apply Odd_ZOdd in H1. rewrite Z.pow_opp_odd with (a := 1). rewrite Z.pow_1_l; lia. auto.
Qed.

Lemma Zpow_neg_nat_odd : forall n z ,
  Nat.Odd n -> (-z) ^ (Z.of_nat n) = -z ^ (Z.of_nat n).
Proof.
  intros n z H1. apply Odd_ZOdd in H1. rewrite Z.pow_opp_odd with (a := z). reflexivity. auto.
Qed.

Lemma Zpow_neg_nat_even : forall n z,
  Nat.Even n -> (-z) ^ (Z.of_nat n) = z ^ (Z.of_nat n).
Proof.
  intros n z H1. apply Even_ZEven in H1. rewrite Z.pow_opp_even. reflexivity. auto.
Qed.

Lemma div_trans : forall a b c,
  (a | b) -> (b | c) -> (a | c).
Proof.
  intros a b c [k1 H1] [k2 H2]. unfold Z.divide. exists (k1 * k2). lia.
Qed.

Close Scope Z_scope.

Lemma div_nonzero: forall x y : R, x <> 0 -> y <> 0 -> x / y <> 0.
Proof.
  intros x y Hx Hy.
  unfold Rdiv.
  apply Rmult_integral_contrapositive_currified; auto.
  apply Rinv_neq_0_compat; auto.
Qed.

Lemma pow_sub : forall (x:R) (n m:nat), x <> 0 -> (n >= m)%nat -> x ^ (n - m) = x ^ n / x ^ m.
Proof.
  intros x n m H1 H2. induction m as [| k IH].
  - simpl. rewrite Nat.sub_0_r. lra.
  - assert (n = S k \/ n >= k)%nat as [H3 | H3] by lia.
    -- rewrite H3. replace (S k - S k)%nat with 0%nat by lia. rewrite pow_O. field. apply pow_nonzero; auto.
    -- apply Rmult_eq_reg_l with (r := x); auto. replace x with (x^1) at 1 by lra. rewrite <- pow_add. replace (1 + (n - S k))%nat with (n - k)%nat by lia.
        rewrite IH; try lia. simpl. field. split; auto. apply pow_nonzero; auto.
Qed.

Definition R_divides (r1 r2 : R) : Prop :=
  exists a b : Z, r1 = IZR a /\ r2 = IZR b /\ (a | b)%Z.

Notation "( x | y )" := (R_divides x y) (at level 0) : R_scope.

Lemma R_divides_refl : (3 | 6)%R.
Proof.
  exists (3%Z), (6%Z); repeat split; try reflexivity. exists 2%Z. reflexivity.
Qed.

Lemma R_divides_plus : forall r r1 r2,
  (r | r1) -> (r | r2) -> (r | (r1 + r2)).
Proof.
  intros r r1 r2 [a [b [H1 [H2 [c H3]]]]] [e [f [H4 [H5 [g H6]]]]]. exists (a%Z), (b + f)%Z. repeat split; auto.
  - rewrite plus_IZR. lra.
  - exists (c + g)%Z. rewrite H3. rewrite H6. apply eq_IZR. rewrite plus_IZR. repeat rewrite mult_IZR. rewrite plus_IZR.
    apply IZR_eq in H6, H3. rewrite mult_IZR in H3, H6. nra. 
Qed.

Lemma IZR_divides : forall a b,
  (a | b)%Z <-> (IZR a | IZR b).
Proof.
  intros a b. split.
  - exists a, b. repeat split; auto.
  - intros [c [d [H1 [H2 H3]]]]. apply eq_IZR in H1, H2. rewrite H1, H2. auto.
Qed.

Lemma divides_neg_R : forall r1 r2,
  (r1 | r2) -> (r1 | -r2).
Proof.
  intros r1 r2 [a [b [H1 [H2 H3]]]]. rewrite H1, H2. rewrite <- opp_IZR. apply IZR_divides. apply Z.divide_opp_r. auto.
Qed.

Lemma sqrt_2_weak_bound : 1.4 < sqrt 2 < 1.5.
Proof.
   split; (apply Rsqr_incrst_0; try lra; unfold Rsqr; repeat rewrite sqrt_sqrt; try lra; apply sqrt_pos).
Qed.

Lemma pow_incrst_0 : forall r1 r2 n,
  r1 > 0 -> r2 > 0 -> r1^n < r2^n -> r1 < r2.
Proof.
  intros r1 r2 n H1 H2 H3. generalize dependent r1. generalize dependent r2. induction n as [| k IH].
  - intros r2 H1 r1 H2 H3. simpl in H3. lra.
  - intros r2 H1 r1 H2 H3. assert (k = 0 \/ k > 0)%nat as [H4 | H4] by lia.
    -- rewrite H4 in H3. simpl in H3. lra.
    -- simpl in H3. pose proof (Rtotal_order r1 r2) as [H5 | [H5 | H5]]; try nra.
       + rewrite H5 in H3. nra.
       + pose proof (pow_incrst_1 r2 r1 k H4 ltac:(lra) H2) as H6. apply Rgt_lt in H5. specialize (H6 H5). assert (r1 ^ k > 0). { apply pow_lt; nra. } nra.
Qed.

Lemma pow_incrst_2 : forall r1 r2 n,
  r1 > 0 -> r2 > 0 -> r1 < r2 -> r1 ^ n <= r2 ^ n.
Proof.
  intros. assert (n = 0 \/ n <> 0)%nat as [H3 | H3] by lia.
  - rewrite H3. simpl. lra.
  - apply Rlt_le. apply pow_incrst_1 with (n := n); try lra; try lia.
Qed.

Lemma pow_incrst_3 : forall r1 r2 n,
  (n > 0)%nat -> r1 > 0 -> r2 > 0 -> r1 ^ n <= r2 ^ n -> r1 <= r2.
Proof.
  intros r1 r2 n H1 H2 H3 H4. generalize dependent r1. generalize dependent r2. induction n as [| k IH].
  - intros r2 H2 r1 H3 H4. lia.
  - intros r2 H2 r1 H3 H4. assert (k = 0 \/ k > 0)%nat as [H5 | H5] by lia.
    -- rewrite H5 in H4. simpl in H4. lra.
    -- simpl in H4. pose proof (Rtotal_order r1 r2) as [H6 | [H6 | H6]]; try nra.
       pose proof (pow_incrst_2 r2 r1 k H2 H3 H6). apply IH; auto. assert (r1 ^ k > 0). { apply pow_lt; nra. } nra.
Qed.

Lemma pow_eq_1 : forall r1 r2 n,
  (n > 0)%nat -> r1 > 0 -> r2 > 0 -> r1 ^ n = r2 ^ n -> r1 = r2.
Proof.
  intros r1 r2 n H1 H2 H3 H4. generalize dependent r1. generalize dependent r2. induction n as [| k IH].
  - intros r2 H2 r1 H3 H4. lia.
  - intros r2 H2 r1 H3 H4. assert (k = 0 \/ k > 0)%nat as [H5 | H5] by lia.
    -- rewrite H5 in H4. simpl in H4. lra.
    -- pose proof (Rtotal_order r1 r2) as [H6 | [H6 | H6]]; auto.
       + apply pow_incrst_1 with (n := S k) in H6; auto; nra.
       + apply pow_incrst_1 with (n := S k) in H6; auto; nra.
Qed.


Lemma cbrt_2_weak_bound : 1.2 < Rpower 2 (1/3) < 1.3.
Proof.
  split; apply pow_incrst_0 with (n := 3%nat); try nra; try apply Rpower_gt_0; try lra; simpl; repeat rewrite Rmult_1_r; repeat rewrite <- Rpower_plus;
  replace (1 / 3 + (1 / 3 + 1 / 3)) with (INR 1); try rewrite Rpower_1; try nra; try nra; simpl; nra.
Qed.

Lemma cons_neq : forall (r1 r2 : R) (t1 t2 : list R),
  r1 :: t1 <> r2 :: t2 -> r1 <> r2 \/ t1 <> t2.
Proof.
 intros r1 r2 t1 t2 H1. destruct (Req_dec r1 r2) as [H2 | H2].
 - right. intros H3. apply H1. rewrite H2. rewrite H3. reflexivity.
 - left. auto.
Qed.

Lemma list_neq_exists_nth_neq : forall (l1 l2 : list R),
  (length l1 = length l2)%nat -> l1 <> l2 -> exists i, (0 <= i <= length l1 - 1)%nat /\ nth i l1 0 <> nth i l2 0.
Proof.
  intros l1 l2 H1 H2. generalize dependent l2. induction l1 as [| h1 t1 IH].
  - intros l2 H1 H2. simpl in H1. apply eq_sym in H1. apply length_zero_iff_nil in H1. apply eq_sym in H1. tauto.
  - intros l2 H1 H2. destruct l2 as [| h2 t2].
    -- simpl in H1. lia.
    -- simpl in H1. assert (H3 : (length t1 = length t2)%nat) by lia. apply cons_neq in H2 as [H2 | H2].
       + exists 0%nat. split; simpl; try lia; auto.
       + specialize (IH t2 H3 H2) as [k [H4 H5]]. exists (S k). assert (H6 : t1 <> []).
         { destruct t1. apply eq_sym in H3. apply length_zero_iff_nil in H3. rewrite <- H3. auto. discriminate. }
         assert (H7 : (length t1 > 0)%nat). { destruct t1. tauto. simpl. lia. } split; simpl in *; try lia; auto.
Qed.

Lemma map_r_neq : forall (l1 l2 : list R) r,
  (length l1 = length l2)%nat -> map (fun x => x * r) l2 <> l1 -> exists i, (0 <= i <= length l2 - 1)%nat /\ r * nth i l2 0 <> nth i l1 0.
Proof.
  intros l1 l2 r H1 H2. assert (H3 : (length l1 = length (map (fun x => x * r)%R l2))%nat) by (rewrite length_map; auto). apply list_neq_exists_nth_neq in H3 as [i [H4 H5]]; auto.
  exists i. split; try lia. intros H6. apply H5. rewrite <- H6. set (f := fun x => x * r). replace 0 with (f 0) at 2 by (unfold f; lra). rewrite map_nth. unfold f. lra.
Qed.

Lemma map_Rmult_0 : forall (l : list R),
  map (fun x => x * 0) l = repeat 0%R (length l).
Proof.
  intros l. induction l as [| h t IH].
  - reflexivity.
  - simpl. rewrite IH. rewrite Rmult_0_r. reflexivity.
Qed.

Lemma nth_cons_f_mult : forall (l1 l2 : list R) (i : nat),
  (length l1 = length l2)%nat -> (nth i l1 0) * (nth i l2 0) = nth i (map (fun x => (fst x) * (snd x)) (combine l1 l2)) 0.
Proof.
  intros l1 l2 i H1. set (f := fun x : R * R => fst x * snd x). replace 0 with (f (0, 0)) at 3 by (compute; lra). rewrite map_nth. rewrite combine_nth; try lia. reflexivity.
Qed.

Lemma exists_pow_2_gt_n : forall n,
  exists m, (2 ^ m > n)%nat.
Proof.
  intros n. induction n as [| n' IH].
  - exists 1%nat. simpl. lia.
  - destruct IH as [m IH]. exists (S m). simpl. lia.
Qed.

Lemma pow_minus : forall a b c,
  a > 0 -> (b > 0)%nat -> (c > 0)%nat -> (b >= c)%nat -> a ^ (b - c) = a ^ b / a ^ c.
Proof.
  intros a b c H1 H2 H3 H4. induction c as [| c' IH].
  - simpl. unfold Rdiv. rewrite Rinv_1. rewrite Rmult_1_r. rewrite Nat.sub_0_r. reflexivity.
  - simpl. replace (a^b / (a * a^c')) with (a^b / a^c' * / a). 2 : { field. split. apply pow_nonzero. lra. lra. }
    assert (c' = 0 \/ c' > 0)%nat as [H5 | H5] by lia.
    -- rewrite H5. simpl. unfold Rdiv. rewrite Rinv_1. rewrite Rmult_1_r. destruct b.
      --- lia.
      --- simpl. rewrite Nat.sub_0_r. field. lra.
    -- rewrite <- IH. 2 : { lia. } 2 : { lia. } replace (b - c')%nat with (S (b - S c'))%nat by lia. simpl. field. lra.
Qed.

Lemma Rle_pow_base : forall a b n,
  0 < a -> 0 < b -> (n > 0)%nat -> a^n <= b^n -> a <= b.
Proof.
  intros a b n H1 H2 H3 H4. induction n as [| k IH].
  - lia.
  - simpl in H4. destruct k.
    -- simpl in H4. lra.
    -- apply IH. lia. simpl. simpl in H4. pose proof Rtotal_order a b as [H5 | [H6 | H7]].
      --- assert (H6 : a^k <= b^k). { apply pow_incr. lra. } assert (H7 : a^k > 0). { apply pow_lt. lra. } nra.
      --- rewrite H6. lra.
      --- assert (H8 : b^k <= a^k). { apply pow_incr. lra. } assert (H9 : b^k > 0). { apply pow_lt. lra. }
          assert (H10 : a * a^k > b * b^k). { nra. } assert (H11 : a * (a * a^k) > b * (b * b^k)). { apply Rmult_gt_0_lt_compat. nra. nra. nra. nra. } nra.
Qed.

Lemma pow_eq_even : ∀ (r1 r2 : ℝ) (m : ℕ),
  (m > 0)%nat → Nat.Even m → r1 ^ m = r2 ^ m → r1 = r2 ∨ r1 = - r2.
Proof.
  intros r1 r2 m H1 H2 H3.
  destruct H2 as [k H4].
  assert (H5 : ∀ x k, x ^ (2 * k) = (x ^ 2) ^ k).
  { intros x k0. induction k0 as [|k0 H6].
    - simpl. ring.
    - replace (2 * S k0)%nat with (S (S (2 * k0)))%nat by lia.
      simpl in *. rewrite H6. ring. }
  assert (H6 : (k > 0)%nat) by lia.
  destruct (Req_dec r1 0) as [H7 | H7].
  - rewrite H7 in H3.
    assert (H8 : 0 ^ m = 0) by (destruct m; [lia | simpl; lra]).
    rewrite H8 in H3.
    destruct (Req_dec r2 0) as [H9 | H9].
    + left. lra.
    + assert (H10 : r2 ^ m ≠ 0) by (apply pow_nonzero; exact H9).
      exfalso. apply H10. lra.
  - destruct (Req_dec r2 0) as [H8 | H8].
    + rewrite H8 in H3.
      assert (H9 : 0 ^ m = 0) by (destruct m; [lia | simpl; lra]).
      rewrite H9 in H3.
      assert (H10 : r1 ^ m ≠ 0) by (apply pow_nonzero; exact H7).
      exfalso. apply H10. lra.
    + assert (H9 : r1 ^ 2 > 0) by (simpl; nra).
      assert (H10 : r2 ^ 2 > 0) by (simpl; nra).
      assert (H11 : (r1 ^ 2) ^ k = (r2 ^ 2) ^ k).
      { rewrite <- 2!H5. rewrite <- H4. exact H3. }
      assert (H12 : r1 ^ 2 = r2 ^ 2).
      { apply pow_eq_1 with (n := k); try assumption. }
      simpl in H12.
      nra.
Qed.

Lemma pow_eq_odd : ∀ (r1 r2 : ℝ) (m : ℕ),
  Nat.Odd m → r1 ^ m = r2 ^ m → r1 = r2.
Proof.
  intros r1 r2 m H1 H2.
  destruct H1 as [k H3].
  assert (H4 : (m > 0)%nat) by lia.
  assert (H5 : (r1 ^ m) ^ 2 = (r2 ^ m) ^ 2) by nra.
  assert (H6 : ∀ x n, (x ^ n) ^ 2 = (x ^ 2) ^ n).
  { intros x n. induction n as [|n H7].
    - simpl. ring.
    - simpl in *. nra. 
  }
  rewrite H6 in H5.
  rewrite H6 in H5.
  destruct (Req_dec r1 0) as [H7 | H7].
  - rewrite H7 in H2.
    assert (H8 : 0 ^ m = 0) by (destruct m; [lia | simpl; lra]).
    rewrite H8 in H2.
    destruct (Req_dec r2 0) as [H9 | H9].
    + lra.
    + assert (H10 : r2 ^ m ≠ 0) by (apply pow_nonzero; exact H9).
      exfalso. apply H10. lra.
  - destruct (Req_dec r2 0) as [H8 | H8].
    + rewrite H8 in H2.
      assert (H9 : 0 ^ m = 0) by (destruct m; [lia | simpl; lra]).
      rewrite H9 in H2.
      assert (H10 : r1 ^ m ≠ 0) by (apply pow_nonzero; exact H7).
      exfalso. apply H10. lra.
    + assert (H9 : r1 ^ 2 > 0) by nra.
      assert (H10 : r2 ^ 2 > 0) by nra.
      assert (H11 : r1 ^ 2 = r2 ^ 2).
      { apply pow_eq_1 with (n := m); try assumption. }
      assert (H12 : r1 = r2 ∨ r1 = - r2) by nra.
      destruct H12 as [H13 | H13].
      * exact H13.
      * rewrite H13 in H2.
        assert (H14 : ∀ x, (- x) ^ m = - (x ^ m)).
        { 
          intro x. rewrite H3.
          replace (2 * k + 1)%nat with (S (2 * k))%nat by lia.
          simpl.
          assert (H15 : ∀ y j, (- y) ^ (2 * j) = y ^ (2 * j)).
          {
            intros y j. induction j as [|j H16].
            - simpl. ring.
            - replace (2 * S j)%nat with (S (S (2 * j)))%nat by lia.
              simpl in *. nra.
          }
          replace (k + (k + 0))%nat with (2 * k)%nat by lia.
          rewrite H15. ring.
        }
        rewrite H14 in H2.
        assert (H15 : r2 ^ m = 0) by nra.
        assert (H16 : r2 ^ m ≠ 0) by (apply pow_nonzero; exact H8).
        exfalso. apply H16. exact H15.
Qed.

Lemma pow_neg_even : forall (x : R) (n : nat),
  Nat.Even n -> (- x) ^ n = x ^ n.
Proof.
  intros x n [k Hk]. subst n.
  induction k as [| k IH].
  - simpl. reflexivity.
  - replace (2 * S k)%nat with (S (S (2 * k)))%nat by lia.
    simpl in *. rewrite IH. lra.
Qed.

Lemma pow_neg_odd : forall (x : R) (n : nat),
  Nat.Odd n -> (- x) ^ n = - (x ^ n).
Proof.
  intros x n [k Hk]. subst n.
  replace (2 * k + 1)%nat with (S (2 * k))%nat by lia.
  simpl. rewrite pow_neg_even.
  - lra.
  - exists k. lia.
Qed.

Lemma not_empty_In_list : forall {T : Type} (l : list T) (x : T),
  List.In x l -> l <> [].
Proof.
  intros T l x H1 H2. destruct l as [| h t]; [ exfalso; auto | discriminate H2].
Qed.

Fixpoint find (l : list ℝ) (r : ℝ) : bool := 
  match l with 
  | [] => false
  | h :: t => if (Req_dec_T h r) then true else find t r
  end.

Lemma find_iff : forall (l : list ℝ) (r : ℝ), find l r = true <-> List.In r l.
Proof.
  intros l r. split; intros H1.
  - induction l as [| h t IH].
    + simpl in H1. discriminate.
    + simpl in H1. destruct (Req_dec_T h r) as [H2 | H2].
      * left. auto.
      * right. apply IH. auto.
  - induction l as [| h t IH].
    + simpl in H1. auto.
    + simpl in H1. destruct H1 as [H2 | H3].
      * subst. simpl. destruct (Req_dec_T r r) as [H4 | H4]; lra.
      * specialize (IH H3). simpl. destruct (Req_dec_T h r) as [H4 | H4]; auto.
Qed.

Lemma find_iff_false : forall (l : list ℝ) (r : ℝ), find l r = false <-> ~List.In r l.
Proof.
  intros l r. pose proof find_iff l r as H1. split; intros H2.
  - intros H3. apply H1 in H3. rewrite H2 in H3. discriminate.
  - destruct (find l r); auto.  exfalso. apply H2. apply H1. reflexivity.
Qed.

Fixpoint get_all_points (l1 l2 : list ℝ) : list ℝ := 
  match l2 with
  | [] => []
  | h :: t => if (find l1 h) then get_all_points l1 t else h :: get_all_points l1 t
  end.

Lemma get_all_points_spec : forall (l1 l2 : list ℝ) (r : ℝ), List.In r (get_all_points l1 l2) <-> (List.In r l2 /\ ~List.In r l1).
Proof.
  intros l1 l2 r. split; intros H1.
  - induction l2 as [| h t IH].
    -- simpl in H1; auto.
    -- destruct (Req_dec r h) as [H2 | H2].
       * subst. split. left. reflexivity. intros H2. simpl in H1. assert (H3 : find l1 h = true). { apply find_iff; auto. }
         destruct (find l1 h). specialize (IH H1) as [IH1 IH2]. apply IH2. auto. inversion H3.
       * simpl in H1. destruct (find l1 h). specialize (IH H1). split; try tauto. right. tauto. simpl in H1. destruct H1 as [H1 | H1]; try lra. 
         specialize (IH H1) as [IH1 IH2]. split; try tauto. right. auto.
  - induction l2 as [| h t IH].
    -- simpl in H1; tauto.
    -- destruct H1 as [H1 H2]. simpl. destruct (Req_dec r h) as [H3 | H3].
       * subst. pose proof find_iff_false l1 h as H4. destruct (find l1 h). rewrite <- H4 in H2. discriminate. left. reflexivity.
       * simpl in H1. destruct H1 as [H1 | H1]; try lra. destruct (find l1 h). tauto. right. tauto.
Qed.


Lemma get_all_points_NoDup : forall (l1 l2 : list ℝ), NoDup l2 -> NoDup (get_all_points l1 l2).
Proof.
  intros l1 l2 H1. induction l2 as [| h t IH].
  - simpl. auto.
  - simpl. destruct (find l1 h).
    -- apply IH. apply NoDup_cons_iff in H1 as [H1 H1']. auto.
    -- apply NoDup_cons_iff. split.
       * intros H2. apply get_all_points_spec in H2 as [H2 _]. apply NoDup_cons_iff in H1 as [H1 H1']. apply H1; auto.
       * apply IH. apply NoDup_cons_iff in H1 as [H1 H1']. auto.
Qed.

Lemma in_split' : forall l (x : R),
  List.In x l -> exists l1 l2, l = l1 ++ [x] ++ l2.
Proof.
  intros l x H1. apply in_split; auto.
Qed.

Lemma list_in_first_app : forall a l1 l2,
  (l1 ++ l2).[0] = a -> l1 <> [] -> List.In a l1.
Proof.
  intros a l1 l2 H1 H2. destruct l1 as [| h t].
  - exfalso. apply H2. reflexivity.
  - simpl in H1. left. auto.
Qed.

Lemma list_in_last_app : forall a l1 l2,
  (l1 ++ l2).[(length l1 + length l2 - 1)] = a -> l2 <> [] -> List.In a l2.
Proof.
  intros a l1 l2 H1 H2. generalize dependent l1. induction l2 as [| h t IH].
  - intros l1 H1. exfalso. apply H2. reflexivity.
  - intros l1 H1. destruct t.
    -- rewrite app_nth2 in H1; [ | simpl; lia].
       replace (length l1 + length [h] - 1 - length l1)%nat with 0%nat in H1 by (simpl; lia).
       simpl in H1. subst. left. reflexivity.
    -- assert (H3 : r :: t <> []). { intros H3. inversion H3. }
       specialize (IH H3 (l1 ++ [h])). right. apply IH.
       replace (length (l1 ++ [h]) + length (r :: t) - 1)%nat with (length l1 + length (h :: r :: t) - 1)%nat.
       2 : { rewrite length_app. simpl. lia.  }
       rewrite <- app_assoc. rewrite <- H1. reflexivity.
Qed.

Lemma last_concat : forall l c,
  l.[(length l - 1)] = c -> l <> [] -> exists l', l = l' ++ [c].
Proof.
  intros l c H1 H2. pose proof exists_last H2 as [l' [x H4]].
  exists l'. rewrite H4 in H1. rewrite app_nth2 in H1.
  2 : { rewrite length_app in *. simpl in *; lia. }
  replace (length (l' ++ [x]) - 1 - length l')%nat with 0%nat in H1.
  2 : { rewrite length_app in *. simpl in *; lia. } simpl in H1. subst. reflexivity.
Qed.

Lemma first_concat : forall l c,
  l.[0] = c -> l <> [] -> exists l', l = [c] ++ l'.
Proof.
  intros l c H1 H2. destruct l as [| h t].
  - exfalso. apply H2. reflexivity.
  - simpl in H1. subst. exists t. reflexivity.
Qed.

Lemma exists_int_gt_inv_scale : forall a b ε,
  a < b -> ε > 0 -> exists z : Z,
    (z > 0)%Z /\ (b - a) / (IZR z) < ε.
Proof.
  intros a b ε H1 H2.
  pose proof archimed (2 * (b - a) / ε) as [H3 H4].
  assert (IZR (up (2 * (b - a) / ε)) - (2 * (b - a)) / ε = 1 \/ IZR (up (2 * (b - a) / ε)) - 2 * (b - a) / ε < 1) as [H5 | H5] by lra.
  - exists (up (2 * (b - a) / ε) - 1)%Z. split.
    -- assert (2 * (b - a) / ε > 0) as H6. { apply Rdiv_pos_pos; lra. } apply Z.lt_gt. apply lt_IZR. rewrite minus_IZR. lra.
    -- rewrite minus_IZR. replace (IZR (up (2 * (b - a) / ε)) -1) with (2 * (b-a)/ε) by lra. apply Rmult_lt_reg_r with (r := ε); try lra.
       field_simplify; nra.
  - exists (up (2 * (b - a) / ε))%Z. split.
    -- assert (2 * (b - a) / ε > 0) as H6. { apply Rdiv_pos_pos; lra. } apply Z.lt_gt. apply lt_IZR. lra.
    -- assert (2 * (b - a) / ε > 0) as H6. { apply Rdiv_pos_pos; lra. } pose proof (Rinv_pos ε ltac:(lra)) as H7.
       apply Rmult_lt_reg_r with (r := IZR (up (2 * (b - a) / ε))); try lra;
       apply Rmult_lt_reg_l with (r := / ε); field_simplify; try lra.
Qed.

Lemma exists_nat_gt_inv_scale : forall a b ε,
  a < b -> ε > 0 -> exists n : nat,
    (n > 0)%nat /\ (b - a) / (INR n) < ε.
Proof.
  intros a b ε H1 H2.
  pose proof exists_int_gt_inv_scale a b ε H1 H2 as [z [H3 H4]].
  exists (Z.to_nat z). split; try lia. rewrite INR_IZR_INZ. rewrite Z2Nat.id. auto. lia.
Qed.

Lemma list_delta_lt_nth_0 : forall a b n,
  nth 0 (map (fun x => ((b - a) / INR n) * x + a) (map INR (seq 0 (n+1)))) a = a.
Proof.
  intros a b n. destruct n; simpl; lra. 
Qed.

Lemma list_delta_lt_nth_n : forall a b n,
  (n <> 0)%nat -> nth n (map (fun x => ((b - a) / INR n) * x + a) (map INR (seq 0 (n+1)))) a = b.
Proof.
  intros a b n H1. set (f := fun x => ((b - a) / INR n) * x + a).
  replace a with (f 0). 2 : { unfold f. rewrite Rmult_0_r. lra. }
  rewrite map_nth. replace 0 with (INR 0) by auto. rewrite map_nth. 
  unfold f. rewrite seq_nth; try lia. solve_R. apply not_0_INR; auto.
Qed.

Lemma a_In_list_delta_lt : forall a b n,
  List.In a (map (fun x => ((b - a) / INR n) * x + a) (map INR (seq 0 (n+1)))).
Proof.
  intros a b n. set (l := map (fun x => ((b - a) / INR n) * x + a) (map INR (seq 0 (n+1)))).
  replace a with (nth 0 l a) in *. 2 : { apply list_delta_lt_nth_0; auto. }
  apply nth_In. unfold l. repeat rewrite length_map. rewrite length_seq. lia.
Qed.

Lemma b_In_list_delta_lt : forall a b n,
  (n <> 0)%nat -> List.In b (map (fun x => ((b - a) / INR n) * x + a) (map INR (seq 0 (n+1)))).
Proof.
  intros a b n H1. set (l := map (fun x => ((b - a) / INR n) * x + a) (map INR (seq 0 (n+1)))).
  replace b with (nth n l a) in *. 2 : { apply list_delta_lt_nth_n; auto. }
  apply nth_In. unfold l. repeat rewrite length_map. rewrite length_seq. lia.
Qed.

Lemma map_nth_in_bounds : forall (A B : Type) (l : list B) (i : nat) (d : B) (d' : A) (f : B -> A),
  (i < length l)%nat ->
  nth i (map f l) d' = f (nth i l d).
Proof.
  intros A B l i d d' f H1. replace (nth i (map f l) d') with (nth i (map f l) (f d)).
  2 : { apply nth_indep. rewrite length_map; lia. } 
  apply map_nth.
Qed.

Lemma list_delta_lt : forall a b i n ε,
  let l := map (fun x => ((b - a) / INR n) * x + a) (map INR (seq 0 (n+1))) in
    (i < length l - 1)%nat -> (n <> 0)%nat -> (b - a) / (INR n) < ε -> l.[(i+1)] - l.[i] < ε.
Proof.
  intros a b i n ε l H1 H2 H3. set (f := fun x => ((b - a) / INR n) * x + a).
  assert (H4 : (length l = n + 1)%nat). {
    unfold l. repeat rewrite length_map. rewrite length_seq. lia.
  }
  unfold l. repeat rewrite map_nth_in_bounds with (d := 0); try (rewrite length_map; rewrite length_seq; lia).
  replace ((map INR (seq 0 (n + 1))).[(i + 1)]) with (INR (i + 1)).
  2 : { rewrite map_nth_in_bounds with (d := 0%nat). 2 : { rewrite length_seq; lia. }
    rewrite seq_nth; try lia. reflexivity. }
  replace ((map INR (seq 0 (n + 1))).[i]) with (INR i).
  2 : { rewrite map_nth_in_bounds with (d := 0%nat). 2 : { rewrite length_seq; lia. }
    rewrite seq_nth; try lia. reflexivity. } solve_R.
Qed.