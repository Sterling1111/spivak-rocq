From Lib Require Import Imports Reals_util Notations.
Open Scope Z_scope.

Definition rational (r : R) : Prop :=
  exists z1 z2 : Z, (r = (IZR z1) / (IZR z2))%R.

Definition irrational (r : R) : Prop :=
  ~ rational r.

Lemma x_neq_0_IZR_den_neq_0 : forall x y z,
  (x <> 0 /\ x = IZR y / IZR z)%R -> z <> 0. 
Proof.
  intros x y z [H1 H2]. assert (z <> 0 \/ z = 0) as [H3 | H3] by lia. auto. rewrite H3 in H2. rewrite Rdiv_0_r in H2. nra.
Qed.

Lemma x_neq_0_IZR_num_neq_0 : forall x y z,
  (x <> 0 /\ x = IZR y / IZR z)%R -> y <> 0.
Proof.
  intros x y z [H1 H2]. assert (y <> 0 \/ y = 0) as [H3 | H3] by lia. auto. rewrite H3 in H2. rewrite Rdiv_0_l in H2. nra.
Qed.

Lemma mult_rational : forall a b,
  rational a -> rational b -> rational (a * b).
Proof.
  intros a b [z1 [z2 H1]] [z3 [z4 H2]].
  assert (a = 0 \/ b = 0 \/ a <> 0 /\ b <> 0)%R as [H3 | [H3 | [H3 H4]]] by lra.
  - exists 0, 1. nra.
  - exists 0, 1. nra.
  - exists (z1 * z3). exists (z2 * z4). rewrite H1. rewrite H2. repeat rewrite mult_IZR. field.
    split; apply not_0_IZR.
    -- apply x_neq_0_IZR_den_neq_0 with (x := b) (y := z3) (z := z4). auto.
    -- apply x_neq_0_IZR_den_neq_0 with (x := a) (y := z1) (z := z2). auto.
Qed.

Lemma plus_rational : forall a b,
  rational a -> rational b -> rational (a + b).
Proof.
  intros a b [z1 [z2 H1]] [z3 [z4 H2]].
  assert ((a = 0 \/ b = 0 \/ a <> 0 /\ b <> 0)%R) as [H3 | [H3 | H3]] by lra.
  - exists z3. exists z4. nra.
  - exists z1. exists z2. nra.
  - assert (H4 : forall x y z, (x <> 0 /\ x = IZR y / IZR z)%R -> z <> 0).
    { intros x y z [H4 H5]. assert (z <> 0 \/ z = 0) as [H6 | H6] by lia. auto. rewrite H6 in H5. rewrite Rdiv_0_r in H5. nra. }
    assert (H5 : z2 <> 0 /\ z4 <> 0). { split. apply H4 with (x := a) (y := z1). tauto. apply H4 with (x := b) (y := z3). tauto. }
    unfold rational. exists (z1 * z4 + z3 * z2). exists (z2 * z4). rewrite H1. rewrite H2. rewrite plus_IZR.
    repeat rewrite mult_IZR. field; split; apply not_0_IZR; lia.
Qed.

Lemma inv_rational : forall a,
  rational a -> rational (/ a).
Proof.
  intros a [z1 [z2 H1]].
  exists z2, z1.
  unfold Rdiv. rewrite <- Rdiv_1_l, H1.
  assert (z1 = 0 \/ z2 = 0 \/ z1 <> 0 /\ z2 <> 0) as [H2 | [H2 | [H2 H3]]] by lia.
  - subst. rewrite Rdiv_0_l, Rdiv_0_r, Rinv_0. solve_R.
  - subst. repeat rewrite Rdiv_0_r. rewrite Rmult_0_l. reflexivity.
  - solve_R; split; apply not_0_IZR; auto.
Qed.

Lemma div_rational : forall a b,
  rational a -> rational b -> rational (a / b).
Proof.
  intros a b H1 H2. unfold Rdiv.
  apply mult_rational; auto. apply inv_rational; auto.
Qed.

Lemma IZR_rational : forall z : Z, rational (IZR z).
Proof.
  intros z. unfold rational. exists z, 1%Z. 
  unfold Rdiv. rewrite Rinv_1, Rmult_1_r. reflexivity.
Qed.

Lemma neg_rational : forall a, rational a -> rational (- a).
Proof.
  intros a [z1 [z2 Ha]].
  exists (-z1)%Z, z2.
  rewrite opp_IZR.
  rewrite Ha.
  lra.
Qed.

Lemma minus_rational : forall a b, rational a -> rational b -> rational (a - b).
Proof.
  intros a b Ha Hb.
  replace (a - b)%R with (a + - b)%R by lra.
  apply plus_rational.
  - exact Ha.
  - apply neg_rational. exact Hb.
Qed.

Create HintDb rat_db.

Hint Resolve IZR_rational plus_rational mult_rational div_rational neg_rational minus_rational inv_rational : rat_db.

Ltac assert_rational expr name :=
  assert (name : rational expr) by auto with rat_db.

Lemma gcd_gt_0 : forall a b : Z, a <> 0 -> b <> 0 -> Z.gcd a b > 0.
Proof.
  intros a b H1 H2. pose proof Z.gcd_nonneg a b. assert (Z.gcd a b = 0 \/ Z.gcd a b > 0) as [H3 | H3] by lia.
  - apply Z.gcd_eq_0_r in H3. lia.
  - auto.
Qed.

Lemma rational_representation : forall r z1 z2,
  z1 <> 0 -> z2 <> 0 ->
  (r = IZR z1 / IZR z2)%R -> exists z1' z2',
    ((r = IZR z1' / IZR z2')%R /\ (forall x, x > 1 -> ~(x | z1') \/ ~(x | z2'))).
Proof.
  intros r z1 z2 H1 H2 H3. exists (z1 / Z.gcd z1 z2). exists (z2 / Z.gcd z1 z2). split.
  - rewrite H3. pose proof Z.gcd_divide_r z1 z2 as H4. pose proof Z.gcd_divide_l z1 z2 as H5.
    unfold Z.divide in H4. unfold Z.divide in H5. destruct H4 as [k1 H4]. destruct H5 as [k2 H5].
    assert (Z.gcd z1 z2 <> 0) as H6 by lia.
    assert (H7 : Z.gcd z1 z2 > 0). { pose proof Z.gcd_nonneg z1 z2. lia. }
    replace (z1 / Z.gcd z1 z2) with (k2). 2 : { rewrite H5 at 1. rewrite Z_div_mult. reflexivity. lia. }
    replace (z2 / Z.gcd z1 z2) with (k1). 2 : { rewrite H4 at 1. rewrite Z_div_mult. reflexivity. lia. }
    rewrite H4. rewrite H5 at 1. repeat rewrite mult_IZR.
    replace ((IZR k2 * IZR (Z.gcd z1 z2) / (IZR k1 * IZR (Z.gcd z1 z2)))%R) with
            ( IZR k2 / IZR k1 * (IZR (Z.gcd z1 z2) / IZR (Z.gcd z1 z2)))%R.
    2 : { field. split. apply not_0_IZR. auto. apply not_0_IZR. lia. }
    rewrite Rdiv_diag. 2 : { apply not_0_IZR. lia. } nra.
  - intros x H4. apply not_and_or. intros [[a H5] [b H6]]. pose proof Z.gcd_divide_l z1 z2 as [c H7].
    pose proof Z.gcd_divide_r z1 z2 as [d H8]. rewrite H7 in H5 at 1. rewrite H8 in H6 at 1.
    rewrite Z_div_mult in H5 by (apply gcd_gt_0; auto). rewrite Z_div_mult in H6 by (apply gcd_gt_0; auto).
    rewrite H5 in H7. rewrite H6 in H8. assert (H9 : Z.divide (x * Z.gcd z1 z2) z1). { rewrite H7 at 2. exists (a). lia. }
    assert (H10 : Z.divide (x * Z.gcd z1 z2) z2). { exists (b). lia. } pose proof Z.gcd_greatest z1 z2 (x * Z.gcd z1 z2) as H11.
    apply H11 in H9. 2 : { auto. } unfold Z.divide in H9. destruct H9 as [k H9]. assert (Z.gcd z1 z2 > 0) as H12 by (apply gcd_gt_0; auto).
    assert (k < 0 \/ k = 0 \/ k > 0) as [H13 | [H13 | H13]] by lia.
    -- nia.
    -- nia.
    -- assert (H14 : k * x * Z.gcd z1 z2 > Z.gcd z1 z2). { rewrite <- Zmult_1_l. apply Zmult_gt_compat_r. lia. lia. }
       nia.
Qed.

Lemma sqrt_rational_neq_0 : forall r z1 z2,
  (r > 0)%R -> sqrt r = (IZR z1 / IZR z2)%R -> (z1 <> 0 /\ z2 <> 0).
Proof.
  intros r z1 z2 H1 H2. split.
  - intros H3. rewrite H3 in H2. rewrite Rdiv_0_l in H2. pose proof sqrt_lt_R0 r. nra.
  - intros H3. rewrite H3 in H2. rewrite Rdiv_0_r in H2. pose proof sqrt_lt_R0 r. nra.
Qed.

Lemma even_pow2 : forall z,
  Z.Even (z * z) -> Z.Even z.
Proof.
  intros z H. pose proof Z.Even_or_Odd z as [H1 | H1].
  - auto.
  - destruct H1 as [k H1]. destruct H as [k' H]. nia.
Qed.

Lemma sqrt_2_irrational : irrational (sqrt 2).
Proof.
  unfold irrational. unfold rational. unfold not. intros [z1 [z2 H1]].
  assert (z1 <> 0 /\ z2 <> 0) as [H2 H3] by (apply sqrt_rational_neq_0 with (r := 2%R); lra).
  assert (H4 : exists z1' z2', ((sqrt 2 = IZR z1' / IZR z2')%R /\ (forall x, x > 1 -> ~(x | z1') \/ ~(x | z2')))).
  { apply rational_representation with (z1 := z1) (z2 := z2). apply H2. apply H3. apply H1. }
  destruct H4 as [z1' [z2' [H4 H5]]]. assert (H6 : (sqrt 2 * sqrt 2 = (IZR z1' / IZR z2') * (IZR z1' / IZR z2'))%R) by nra.
  rewrite sqrt_sqrt in H6. 2 : { lra. } assert (H7 : (z1' <> 0 /\ z2' <> 0)) by (apply sqrt_rational_neq_0 with (r := 2%R); lra).
  destruct H7 as [H7 H8]. assert (H9 : (IZR z1' <> 0)%R) by (apply not_0_IZR; auto). assert (H10 : (IZR z2' <> 0)%R) by (apply not_0_IZR; auto).
  replace (IZR z1' / IZR z2' * (IZR z1' / IZR z2'))%R with ((IZR z1')^2 / (IZR z2')^2)%R in H6.
  2 : { field. auto. } apply Rmult_eq_compat_r with (r := ((IZR z2')^2)%R) in H6.
  replace ((IZR z1' ^ 2 / IZR z2' ^ 2 * IZR z2' ^ 2)%R) with ((IZR z1')^2)%R in H6.
  2 : { field. auto. } simpl in H6. repeat rewrite Rmult_1_r in H6. repeat rewrite <- mult_IZR in H6.
  apply eq_IZR in H6. assert (H11 : Z.Even (z1' * z1')). { exists (z2' * z2'). lia. }
  apply even_pow2 in H11. destruct H11 as [k1 H11]. assert (H12 : Z.Even (z2' * z2')). { exists (k1 * k1). nia. }
  apply even_pow2 in H12. destruct H12 as [k2 H12]. specialize (H5 2). destruct H5 as [H5 | H5].
  { lia. } { apply H5. unfold Z.divide. exists (k1). lia. } { apply H5. unfold Z.divide. exists (k2). lia. }
Qed.

Lemma rational_plus_rev : forall a b,
  rational (a + b) -> rational a -> rational b.
Proof.
  intros a b H1 H2. unfold rational in *. destruct H1 as [z1 [z2 H1]]. destruct H2 as [z3 [z4 H2]].
  assert (a = 0 /\ b = 0 \/ a = 0 /\ b <> 0 \/ a <> 0 /\ b = 0 \/ a <> 0 /\ b <> 0)%R as [H3 | [H3 | [H3 | H3]]] by lra.
  - exists 0, 0. lra.
  - exists z1, z2. lra.
  - exists 0, 0. lra.
  - assert (a + b = 0 \/ a + b <> 0)%R as [H4 | H4] by lra.
    -- exists (-z3), z4. replace (-z3) with (-1 * z3) by lia. rewrite mult_IZR. lra.
    -- exists (z1 * z4 - z2 * z3), (z2 * z4).  rewrite minus_IZR. repeat rewrite mult_IZR. pose proof H1 as H1'. rewrite H2 in H1.
       apply Rminus_eq_compat_r with (r := (IZR z3 / IZR z4)%R) in H1. replace ((IZR z3 / IZR z4 + b - IZR z3 / IZR z4)%R) with b in H1 by lra.
       rewrite H1.  destruct H3 as [H3 H3']. 
       field; split; apply not_0_IZR; try apply x_neq_0_IZR_den_neq_0 with (x := (a + b)%R) (y := z1) (z := z2); try apply x_neq_0_IZR_den_neq_0 with (x := a) (y := z3) (z := z4); auto.
Qed.

Lemma rational_mult_rev : forall a b,
  (a <> 0)%R -> rational (a * b) -> rational a -> rational b.
Proof.
  intros a b H1 H2 H3. unfold rational in *. destruct H2 as [z1 [z2 H2]]. destruct H3 as [z3 [z4 H3]].
  assert (a = 0 /\ b = 0 \/ a = 0 /\ b <> 0 \/ a <> 0 /\ b = 0 \/ a <> 0 /\ b <> 0)%R as [H4 | [H4 | [H4 | H4]]] by lra.
  - lra.
  - lra.
  - exists 0, 0. lra.
  - assert (a * b = 0 \/ a * b <> 0)%R as [H5 | H5] by lra.
    -- nra.
    -- exists (z1 * z4), (z2 * z3). repeat rewrite mult_IZR. pose proof H2 as H2'. rewrite H3 in H2.
       assert (H6 : IZR z1 <> 0%R). { apply not_0_IZR. apply x_neq_0_IZR_num_neq_0 with (x := (a * b)%R) (y := z1) (z := z2); split; auto. }
       assert (H7 : IZR z3 <> 0%R). { apply not_0_IZR. apply x_neq_0_IZR_num_neq_0 with (x := a) (y := z3) (z := z4); split; auto. }
       assert (H8 : IZR z2 <> 0%R). { apply not_0_IZR. apply x_neq_0_IZR_den_neq_0 with (x := (a * b)%R) (y := z1) (z := z2); split; auto. }
       assert (H9 : IZR z4 <> 0%R). { apply not_0_IZR. apply x_neq_0_IZR_den_neq_0 with (x := a) (y := z3) (z := z4); split; auto. }
       apply Rmult_eq_compat_r with (r := (IZR z4 / IZR z3)%R) in H2. replace ((IZR z3 / IZR z4 * b * (IZR z4 / IZR z3))%R) with b in H2 by (field; lra).
       rewrite H2. field; auto.
Qed.

Open Scope R_scope.

Lemma exists_irrational_between : forall a b,
  a < b -> exists c, (a < c < b)%R /\ irrational c.
Proof.
  intros a b H1.
  assert (H2 : 0 < b - a) by lra.
  assert (H3 : 0 < 1 / (b - a)) by (apply Rdiv_pos_pos; lra).
  pose proof (archimed (1 / (b - a))) as [H4 H5].
  set (z2 := up (1 / (b - a))).
  assert (H6 : 0 < IZR z2). { unfold z2. lra. }
  pose proof (archimed ((a - sqrt 2) * IZR z2)) as [H7 H8].
  set (z1 := up ((a - sqrt 2) * IZR z2)).
  set (q := IZR z1 / IZR z2).
  exists (q + sqrt 2).
  split.
  - assert (H9 : a - sqrt 2 < q).
    { unfold q. apply Rmult_lt_reg_r with (r := IZR z2); try lra.
      replace (IZR z1 / IZR z2 * IZR z2) with (IZR z1) by (field; lra).
      solve_R. }
    assert (H10 : q < b - sqrt 2).
    { unfold q. apply Rmult_lt_reg_r with (r := IZR z2); try lra.
      replace (IZR z1 / IZR z2 * IZR z2) with (IZR z1) by (field; lra).
      assert (H11 : 1 < (b - a) * IZR z2).
      { apply Rmult_lt_reg_l with (r := 1 / (b - a)); try lra.
        replace (1 / (b - a) * 1) with (1 / (b - a)) by lra.
        replace (1 / (b - a) * ((b - a) * IZR z2)) with (IZR z2) by (field; lra).
        solve_R. } unfold z1, z2 in *. lra.
     }
    lra.
  - unfold irrational, not in *. intros H9.
    assert (H10 : rational (sqrt 2)).
    { apply rational_plus_rev with (a := q).
      - exact H9.
      - unfold rational, q. exists z1, z2. reflexivity. }
    apply sqrt_2_irrational. exact H10.
Qed.

Lemma exists_rational_between : forall a b,
  a < b -> exists c, (a < c < b)%R /\ rational c.
Proof.
  intros a b H1.
  assert (H2 : 0 < b - a) by lra.
  assert (H3 : 0 < 1 / (b - a)) by (apply Rdiv_pos_pos; lra).
  pose proof (archimed (1 / (b - a))) as [H4 H5].
  set (z2 := up (1 / (b - a))).
  assert (H6 : 0 < IZR z2). { unfold z2. lra. }
  
  pose proof (archimed (a * IZR z2)) as [H7 H8].
  set (z1 := up (a * IZR z2)).
  set (q := IZR z1 / IZR z2).
  
  exists q.
  split.
  - assert (H9 : a < q).
    { unfold q. apply Rmult_lt_reg_r with (r := IZR z2); try lra.
      replace (IZR z1 / IZR z2 * IZR z2) with (IZR z1) by (field; lra).
      unfold z1 in *. lra. }
    assert (H10 : q < b).
    { unfold q. apply Rmult_lt_reg_r with (r := IZR z2); try lra.
      replace (IZR z1 / IZR z2 * IZR z2) with (IZR z1) by (field; lra).
      assert (H11 : 1 < (b - a) * IZR z2).
      { apply Rmult_lt_reg_l with (r := 1 / (b - a)); try lra.
        replace (1 / (b - a) * 1) with (1 / (b - a)) by lra.
        replace (1 / (b - a) * ((b - a) * IZR z2)) with (IZR z2) by (field; lra).
        unfold z2 in *. lra. }
      unfold z1, z2 in *. lra. }
    lra.
  - unfold rational, q. exists z1, z2. reflexivity.
Qed.

Lemma irrational_square_imp_irrational : forall r,
  irrational (r^2) -> irrational r.
Proof.
  intros r H1. pose proof classic (rational r) as [H2 | H2]; auto.
  intros _. apply H1. replace (r^2) with (r * r) by lra.
  apply mult_rational; auto.
Qed.

Lemma rational_representation_positive : forall r,
  rational r ->
  r > 0 ->
  exists a b : Z, r = a / b /\ a > 0 /\ b > 0.
Proof.
  intros r [a [b H1]] H2.
  pose proof Rtotal_order a 0 as [H3 | [H3 | H3]]; pose proof Rtotal_order b 0 as [H4 | [H4 | H4]];
  pose proof Rdiv_neg_neg a b as H5; pose proof Rdiv_pos_pos a b as H6; pose proof Rdiv_neg_pos a b as H7; pose proof Rdiv_pos_neg a b as H8;
  try nra; try (rewrite H4 in H1; rewrite Rdiv_0_r in H1; lra).
  - exists (- a)%Z, (- b)%Z. repeat rewrite opp_IZR. split; try nra. rewrite H1. field. nra.
  - exists a, b. auto.
Qed.

Module RationalNotations.
  Declare Scope rational_scope.
  Delimit Scope rational_scope with rat.

  Definition R_q : R -> Prop := fun r => exists q : Q, r = Q2R q.

  Notation "'Q'" := R_q (at level 0) : rational_scope.
  Notation "'ℚ'" := R_q (at level 0) : rational_scope.

End RationalNotations.

Import RationalNotations.
Open Scope rational_scope.

Lemma rational_iff : forall r,
  irrational r <-> (~ Ensembles.In R (Q%rat) r).
Proof.
  intros r. unfold irrational, rational, Ensembles.In. split.
  - intros H1 [q H2]. apply H1. destruct q as [n d].
    exists n, (Z.pos d). rewrite H2. unfold Q2R. reflexivity.
  - intros H1 [z1 [z2 H2]]. apply H1.
    assert (H3 : (z2 = 0 \/ z2 > 0 \/ z2 < 0)%Z) by lia. destruct H3 as [H3 | [H3 | H3]].
    + exists (0 # 1). rewrite H2, H3. unfold Q2R. simpl. rewrite Rdiv_0_r. lra.
    + exists (z1 # Z.to_pos z2). rewrite H2. unfold Q2R. simpl. rewrite Z2Pos.id; try lia. reflexivity.
    + exists ((-z1) # Z.to_pos (-z2)). rewrite H2. unfold Q2R. simpl. rewrite Z2Pos.id; try lia.
      repeat rewrite opp_IZR. field. apply not_0_IZR. lia.
Qed.

Close Scope rational_scope.